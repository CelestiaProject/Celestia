# Script di dimonstrazione di Celestia
# traduzione italiana: Mauro Santandrea
# email: mausan66@tiscali.it

{
	timerate { rate 0 }
	labels { clear "planets|minorplanets|stars|constellations" }
	renderflags { set "stars|planets"
                      clear "constellations|orbits|cloudmaps" }
	print { text "Inizia la dimostrazione . . .\nPremere ESC per terminare." origin "center" duration 2 }
	wait { duration 2.0 }

	print { text "Partiamo dai dintorni di casa . . ." row -3 }
	select { object "Sol/Earth" }
	cancel {}
	# goto { time 0 distance 3 upframe "ecliptical" }
	# gotolonglat { time 0 distance 3 longitude -122 latitude 47 }
	synchronous {}
	gotoloc { time 0.0 position [ 0 0 20000 ] }
	wait { duration 0.1 }
	center { time 0.1 }
	wait { duration 1.0 }
	follow {}

	print { text "Stiamo orbitando intorno alla Terra ad un'altitudine di circa 12500km." row -3 duration 5 }
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	print { text "La Terra ha un aspetto molto più familiare, con le nuvole." row -3}
	wait { duration 0.1 }
	renderflags { set "cloudmaps" }
	orbit { axis [ 0 1 0 ] rate 30 duration 6 }

	print { text "Prossima fermata: la Luna." row -3 }
	select { object "Moon" }
	goto { time 5 distance 4 upframe "equatorial" }
	wait { duration 5.5 }
	print { text "Osserviamo la Terra ed il Sole, mentre orbitiamo intorno alla Luna" row -3}
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	
	print { text "Rotta verso il Sole." row -3}
	select { object "Sol" }
	goto { time 8 distance 12 upframe "equatorial" up [ 0 1 0 ] }
	wait { duration 8.5 }
	print { text "A questa distanza, le macchie solari sono ben visibili sulla superficie del Sole." row -3}
	orbit { axis [ 0 1 0 ] rate 20 duration 10 }

	print { text "Allontaniamoci per avere una panoramica del sistema solare interno." row -3}
	orbit { axis [ 1 0 0 ] rate 45 duration 2 }
	renderflags { set "orbits" }
	changedistance { duration 4.0 rate 1.0 }
	
	print { text "Visualizziamo i nomi dei pianeti . . ." row -3}
	labels { set "planets" }
	wait { duration 1.0 }
	print { text "Possiamo aumentare la velocità del tempo per osservare i pianeti orbitare intorno al sole." row -3}
	timerate { rate 2592000 }
	wait { duration 3.0 }
	print { text "Nella simulazione, ogni secondo corrisponde ad un mese reale." row -3}
	wait { duration 12.0 }
	timerate { rate 1 }
	print { text "Ora il tempo è stato fermato completamente." row -3}

	wait { duration 1.0 }
	print { text "La nostra prossima destinazione è Saturno." row -3}
	select { object "Saturn" }
	center { time 2 }
	wait { duration 2 }
	goto { time 8 distance 6 up [ 0 1 0 ] upframe "equatorial" }
	wait { duration 6.5 }
	renderflags { clear "orbits" }
	labels { clear "planets" }
	print { text "Alcune delle lune di Saturno sono visibili come punti brillanti" row -3 duration 3}
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	
	select { object "Mimas" }
	goto { time 5 distance 4 upframe "equatorial" }
	print { text "La caratteristica principale della luna di Saturno Mimante è l'enorme cratere d'impatto Herschel." row -3 duration 9 }
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	changedistance { duration 6.0 rate 0.5 }

	select { object "Sol" }
	center { time 2 }
	print { text "Notate come appare debole il Sole a questa distanza." row -3 }
	wait { duration 2 }

	print { text "Diamo uno sguardo alle stelle nel cielo." row -3 duration 2 }
	wait { duration 2 }
	select { object "Alpha UMa" }
	center { time 2 }
	print { text "Gli abitanti dell Emisfero Boreale riconosceranno il Gran Carro, nella costellazione dell'Orsa Maggiore." row -3 duration 3 }
	wait { duration 4 }

	select { object "Polaris" }
	center { time 2 }
	wait { duration 2 }
	print { text "Ora siamo centrati sulla Polare, la Stella che indica il Polo Nord." row -3}
	wait { duration 1 }
	labels { set "stars" }
	wait { duration 2 }
	print { text "La Polare fa parte dell'asterismo chiamato il Piccolo Carro." row -3}
	wait { duration 2 }
	print { text "Per aiutarci ad orientarci nel cielo, Celestia può tracciare i disegni delle costellazioni . . ." row -3}
	renderflags { set "constellations" }
	wait { duration 2 }
	print { text ". . . ed i nomi delle costellazioni" row -3}
	labels { set "constellations" }
	wait { duration 2 }

	select { object "Alnilam" }
	center { time 4 }
	wait { duration 2 }
	print { text "Orione è la costellazione più riconoscibile del cielo." row -3 duration 3 }
	wait { duration 4 }

	select { object "Beta Cru" }
	center { time 4 }
	wait { duration 2 }
	print { text "La Croce del Sud è una vista familiare nei cieli dell'Emisfero Australe." row -3 duration 4 }
	wait { duration 4 }

	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 7 }
	print { text "Abilitiamo il rendering delle galassie, in modo da vedere la Via Lattea." row -3 duration 4 }
	renderflags { set "galaxies" }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 14 }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 10 }

	select { object "Antares" }
	center { time 5 }
	wait { duration 3 }
	print { text "Ora viaggeremo fino ad Antares, una gigante rossa nello Scorpione." row -3 duration 5 }
	wait { duration 2 }
	renderflags { clear "constellations" }
	labels { clear "constellations|stars" }
	wait { duration 1 }
	goto { time 8 distance 200 }
	wait { duration 8.5 }
	goto { time 5 distance 10 }
	wait { duration 5.0 }
print { text "Pur essendo lontani da Antares più di 10 volte la distanza della Terra dal Sole, \nugualmente la massiccia gigante rossa incombe enorme nel cielo." row -3}
	wait { duration 4.0 }

	print { text "Allontaniamoci ancora per avere la visione d'insieme . . ." row -3}
	changedistance { duration 10.0 rate 2.0 }

	select { object "Milky Way" }
	print { text "Stiamo osservando l'intera nostra galassia, la Via Lattea." row -3 duration 6 }
	orbit { axis [ 1 0 0 ] rate 30 duration 16.0 }
	print { text "È ora di tornare a casa . . ." row -3}

	select { object "Sol/Earth" }
	goto { time 20 distance 10 upframe "equatorial" }
	wait { duration 20.0 }
	print { text "Dimostrazione completata." row -3}
	wait { duration 3.0 }
}
