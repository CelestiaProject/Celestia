{
	timerate { rate 0 }
	labels { clear "planets|minorplanets|stars|constellations" }
	renderflags { set "stars|planets"
                      clear "constellations|orbits|cloudmaps" }
	print { text "Начало на демонстрацията...\nНатиснете „Esc“ за край." origin "center" duration 2 }
	wait { duration 2.0 }

	print { text "Нека да започнем от нашия дом..." row -3 }
	select { object "Sol/Earth" }
	cancel {}
	# goto { time 0 distance 3 upframe "ecliptical" }
	# gotolonglat { time 0 distance 3 longitude -122 latitude 47 }
	synchronous {}
	gotoloc { time 0.0 position [ 0 0 20000 ] }
	wait { duration 0.1 }
	center { time 0.1 }
	wait { duration 1.0 }
	follow {}

	print { text "В момента се намираме на 12 500 км. над Земята." row -3 duration 5 }
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	print { text "Като добавим и облаците, Земята изглежда по-позната." row -3}
	wait { duration 0.1 }
	renderflags { set "cloudmaps" }
	orbit { axis [ 0 1 0 ] rate 30 duration 6 }

	print { text "Следваща спирка: Луната" row -3 }
	select { object "Moon" }
	goto { time 5 distance 4 upframe "equatorial" }
	wait { duration 5.5 }
	print { text "Оглеждайте се за Земята и Слънцето, докато обикаляме около Луната." row -3}
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	
	print { text "Напред към Слънцето!" row -3}
	select { object "Sol" }
	goto { time 8 distance 12 upframe "equatorial" up [ 0 1 0 ] }
	wait { duration 8.5 }
	print { text "От това разстояние може да видим тъмните слънчеви петна по повърхността му." row -3}
	orbit { axis [ 0 1 0 ] rate 20 duration 10 }

	print { text "Нека да се отдалечим и разгледаме вътрешната част на Слънчевата система." row -3}
	orbit { axis [ 1 0 0 ] rate 45 duration 2 }
	renderflags { set "orbits" }
	changedistance { duration 4.0 rate 1.0 }
	
	print { text "Да включим имената на планетите..." row -3}
	labels { set "planets" }
	wait { duration 1.0 }
	print { text "Може да ускорим времето за да видим как планетите обикалят около Слънцето." row -3}
	timerate { rate 2592000 }
	wait { duration 3.0 }
	print { text "Всяка секунда в реално време е равна на един месец в симулацията." row -3}
	wait { duration 12.0 }
	timerate { rate 1 }
	print { text "В момента, времето е спряно." row -3}

	wait { duration 1.0 }
	print { text "Следваща спирка: Сатурн" row -3}
	select { object "Saturn" }
	center { time 2 }
	wait { duration 2 }
	goto { time 8 distance 6 up [ 0 1 0 ] upframe "equatorial" }
	wait { duration 6.5 }
	renderflags { clear "orbits" }
	labels { clear "planets" }
	print { text "Няколко от спътниците на Сатурн са видими като ярки точки." row -3 duration 3}
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	
	select { object "Mimas" }
	goto { time 5 distance 4 upframe "equatorial" }
	print { text "Най-интересната характеристика на Мимас е огромният кратер Хершел." row -3 duration 9 }
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	changedistance { duration 6.0 rate 0.5 }

	select { object "Sol" }
	center { time 2 }
	print { text "Вижте колко малко изглежда Слънцето от това разстояние." row -3 }
	wait { duration 2 }

	print { text "Нека да разгледаме и другите звезди." row -3 duration 2 }
	wait { duration 2 }
	select { object "Alpha UMa" }
	center { time 2 }
	print { text "Ако живеете в северното полукълбо, ще разпознаете Волската кола в съзвездието Голямата мечка." row -3 duration 3 }
	wait { duration 4 }

	select { object "Polaris" }
	center { time 2 }
	wait { duration 2 }
	print { text "Това е Поларис, известна още като Северната звезда." row -3}
	wait { duration 1 }
	labels { set "stars" }
	wait { duration 2 }
	print { text "Поларис е част от Малката мечка." row -3}
	wait { duration 2 }
	print { text "За по-добра ориентация в небето, „Celestia“ може да активира очертанията на съзвездията..." row -3}
	renderflags { set "constellations" }
	wait { duration 2 }
	print { text "...и имената на съзвездията." row -3}
	labels { set "constellations" }
	wait { duration 2 }

	select { object "Alnilam" }
	center { time 4 }
	wait { duration 2 }
	print { text "Орион е едно от най-известните съзвездия в небето." row -3 duration 3 }
	wait { duration 4 }

	select { object "Beta Cru" }
	center { time 4 }
	wait { duration 2 }
	print { text "Ако живеете в южното полукълбо, ще разпознаете съзвездието Южен кръст." row -3 duration 4 }
	wait { duration 4 }

	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 7 }
	print { text "Нека да включим показването на галактиките за да видим Млечния път." row -3 duration 4 }
	renderflags { set "galaxies" }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 14 }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 10 }

	select { object "Antares" }
	center { time 5 }
	wait { duration 3 }
	print { text "Сега ще посетим Антарес, това е звезда червен гигант в съзвездието Скорпион." row -3 duration 5 }
	wait { duration 2 }
	renderflags { clear "constellations" }
	labels { clear "constellations|stars" }
	wait { duration 1 }
	goto { time 8 distance 200 }
	wait { duration 8.5 }
	goto { time 5 distance 10 }
	wait { duration 5.0 }
	print { text "Въпреки че сме 10 пъти по-далече от Антарес\nотколкото Земята е от Слънцето, масивната звезда червен гигант изглежда застрашително голяма." row -3}
	wait { duration 4.0 }

	print { text "Нека да се отдалечим за да видим как изглежда нашата галактика..." row -3}
	changedistance { duration 10.0 rate 2.0 }

	select { object "Milky Way" }
	print { text "Това е Млечният път." row -3 duration 6 }
	orbit { axis [ 1 0 0 ] rate 30 duration 16.0 }
	print { text "Време е да се прибираме..." row -3}

	select { object "Sol/Earth" }
	goto { time 20 distance 10 upframe "equatorial" }
	wait { duration 20.0 }
	print { text "Демонстрацията е завършена." row -3}
	wait { duration 3.0 }
}
